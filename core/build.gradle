jar.baseName = "imc4j"

dependencies {
    compile 'com.eclipsesource.minimal-json:minimal-json:0.9.5'
    compile 'javax.xml.bind:jaxb-api:2.3.0'
    compile 'javax.annotation:javax.annotation-api:1.3.2'
    compile 'io.vertx:vertx-core:3.8.3'    

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task dist(type: Jar) {
    description = 'Generate fatjar for IMC API'
    group = 'Distribution'
    from {configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
    archiveBaseName = "imc4j"
    destinationDirectory = new java.io.File("$rootDir/dist")
}

task pcreator(type: Jar) {
    description = 'Generate pcreator.jar'
    group = 'Distribution'
    from {configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest {
        attributes 'Main-Class': 'pt.lsts.imc4j.util.PlanCreator'
    }
    with jar
    archiveBaseName = "pcreator"
    destinationDirectory = new java.io.File("$rootDir/dist")
}

task gw(type: JavaExec) {
    group = "Execution"
    description = "Create a JSON gateway"
    classpath = sourceSets.main.runtimeClasspath
    main = "pt.lsts.imc4j.net.JsonGateway"
    args = ["8009"]
}
